apply plugin: 'com.android.library'
apply plugin: 'android-eclipse'
apply plugin: 'maven-publish'
apply plugin: 'digital.wup.android-maven-publish'

android {
    compileSdk 31

    namespace = "com.github.ginvavilon.traghentto.android.glide"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api project(':android')
    implementation project(':utils:stream')
    implementation project(':utils:logger')
    compileOnly 'com.github.bumptech.glide:glide:4.8.0'
    compileOnly 'com.github.bumptech.glide:annotations:4.8.0'
    
}





eclipse {
    project {
        name = 'Traghentto-Android-Glide'
    }
}

task androidJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
 }
afterEvaluate {
    project.tasks.androidJavadocs.classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task jar(type: Jar, dependsOn: 'bundleLibCompileToJarRelease') {
    from zipTree("$buildDir/intermediates/compile_library_classes_jar/release/classes.jar")
}

configurations {
    mavenDeployer
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

configurePublishing(project,'android-glide','android-glide','Tragnetto-Android-Glide',DESCRIPTION,components.android,[jar,sourcesJar,javadocJar]) 
