apply plugin: 'com.android.library'
apply plugin: 'android-eclipse'
apply plugin: 'maven-publish'
apply plugin: 'digital.wup.android-maven-publish'

android {
    compileSdk 31

    namespace = "com.github.ginvavilon.traghentto.android.play.assets"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api project(':base')
    api project(':common')
    implementation project(':android')
    implementation project(':utils:logger')
    implementation project(':utils:public')
    implementation "com.google.android.play:asset-delivery:2.1.0"
    implementation project(path: ':utils:io')
}


eclipse {
    project {
        name = 'Traghentto-Android-GP-Assets'
    }
}

task androidJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
 }
afterEvaluate {
    project.tasks.androidJavadocs.classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task jar(type: Jar, dependsOn: 'bundleLibCompileToJarRelease') {
    from zipTree("$buildDir/intermediates/compile_library_classes_jar/release/classes.jar")
}

configurations {
    mavenDeployer
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

configurePublishing(project,'google-play-assets','google-play-assets','Tragnetto-Google-Play-Assets',DESCRIPTION,components.android,[jar,sourcesJar,javadocJar]) 
